buildscript {
    ext.kotlin_version = '1.1.2-5'
    ext.spring_boot_version = '1.5.3.RELEASE'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

configurations {
    ktlint
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: "kotlin-jpa"
apply plugin: 'org.springframework.boot'
apply plugin: 'application'

jar {
    baseName = 'space-mines-api'
    version = '1.0'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'

    compile 'org.postgresql:postgresql:9.4.1212'
    compile 'com.zaxxer:HikariCP:2.6.0'

    testCompile("org.springframework.boot:spring-boot-starter-test")

    ktlint "com.github.shyiko:ktlint:0.30.0"
    ktlint "me.cassiano:ktlint-html-reporter:0.2.0"
}

bootRun {
    systemProperty "spring.profiles.active", "dev"
}

test {
    systemProperty "spring.profiles.active", "test"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    // Print to console
    args "src/**/*.kt", "--verbose", "--reporter=plain?group_by_file"
    // generate HTML report
    args "--reporter=html,artifact=me.cassiano:ktlint-html-reporter:0.2.0,output=${buildDir}/ktlint.html"

}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}